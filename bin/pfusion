#!/usr/bin/env perl
# TODO: RCL 2011-09-09 Intro header
#
use strict;
use warnings;
use YAML qw/LoadFile DumpFile Dump/;
use Getopt::Long;
use File::Spec::Functions qw/catfile/;
use Google::Fusion;
use Term::ReadLine;
use File::HomeDir;
use Carp;
use Try::Tiny;

my %params;
GetOptions( \%params,
    'client_id=s',
    'client_secret=s',
    'access_code=s',
    'refresh_token=s',
    'access_token=s',
    'config=s',
    'interactive',
    );

# Default params
%params = (
    token_store  => catfile( File::HomeDir->my_home(), '.fusion.auth' ),
    query_cache  => '/tmp/query_cache',
    interactive => 1,
    %params );

# Clean out any undefined values
%params = map{ $_ => $params{$_} }
    grep{ $params{$_} }
    keys %params;

# See if there's a config file in the users home, or a config file defined
my $conf_file = $params{config} || catfile( File::HomeDir->my_home() , '.fusion' );
delete( $params{config} );
if( -f $conf_file ){
    my $file_params = LoadFile( $conf_file );
    %params = (
        %{ $file_params },
        %params,
        );
}

# Initialise the Fusion object and force the auth_client to be built to see
# if enough parameters were given (it's lazy by default)
my $fusion = undef;
try{
    $fusion = Google::Fusion->new( %params );
    $fusion->auth_client();
}catch{
    print $_;
    # TODO: RCL 2011-09-09 Make poddoc
    print "Help to come here...\n";
    exit;
};

# Set up the terminal readline controller
my $term = Term::ReadLine->new('Fusion');
my $prompt = "fusion> ";
my $OUT = $term->OUT || \*STDOUT;
binmode $OUT, ':utf8';
my $command = '';

# Now loop handling the commands
COMMAND:
while ( defined ( $command = $term->readline( $prompt ) ) ) {
    warn $@ if $@;
    if( $command =~ m/^\s*$/ ){
        next COMMAND;
    }
    $term->addhistory($command); 
    if( $command =~ m/^\.(.*)$/ or $command =~ m/^(help)$/i ){
        my $rtn = local_command( $1 );
        if( not $rtn ){
            last COMMAND;
        }

    }else{
        my $result = $fusion->query( $command );
        #print Dump( $result );
        my @rows = @{ $result->rows };

        # Print the result
        print_result( $OUT, $result );
        
    }
}
# If exit was with [ctrl]-d, then there will be no last command, so add a newline.
printf "%sExiting fusion.\n", ( $command ? '' : "\n" );
exit( 0 );

# Accepts a filehandle and a reference to an array of array references
sub print_result {
    my $fh = shift;
    my $result = shift;

    print "\n";

    my $fmt = ' | ';
    foreach( 0 .. $result->num_columns - 1 ){
        # TODO: RCL 2011-09-08 Add some handling here so that if recognized that all elements of
        # a column are numbers they be right justified
        $fmt .= '%-' . $result->max_lengths->[$_] . 's | ';
    }
    $fmt .= "\n";
    
    if( $result->has_headers ){
        # print out the columns, and the separator
        printf $fh $fmt, @{ $result->columns };
        printf $fh $fmt, map{ '=' x $result->max_lengths->[$_] } 0 .. $result->num_columns - 1;
    }

    foreach my $rownum( 0 .. scalar( @{ $result->rows } ) - 1 ){
        my $row = $result->rows->[$rownum];
        printf $fh $fmt, @{ $row };

    }
    print "\n";
    printf "%s line%s in %0.4fs (auth: %0.4fs)\n",
        $result->num_rows,
        ( $result->num_rows > 1 ? 's' : '' ),
        $result->total_time,
        $result->auth_time;
}

# Handle local commands (stuff which is not sent to the Fusion server
sub local_command {
    my $command = shift;
    if( $command =~ m/^(exit|quit)$/i ){
        return 0;
    }elsif( $command =~ m/^help$/i ){
        print_local_help();
    }elsif( $command =~ m/^source (.+)$/ ){
        foreach my $result( @{ local_source( $1 ) } ){
            printf "Success: %s\n", $result->query;
        }
    }else{
        print "I don't know how to do that...\n";
    }
    return 1;
}

# The user called the local help command... give them some help baby!
sub print_local_help {
    my %commands = (
        help    => 'show the internal commands',
        quit    => 'exit the application',
        source  => 'read in a source file',
        'exit'  => 'exit the application',
        );
    # All local commands start with a '.'
    foreach( sort keys %commands ){
        printf ".%-10s %s\n", $_, $commands{$_};
    }
}

sub local_source {
    my $source_file = shift;
    printf "Reading source file: %s\n", $source_file;
    open( my $fh, '<', $source_file ) or die( $! );
    my @lines;
    while( my $line = readline( $fh ) ){
        chomp( $line );
        push( @lines, $line );
    }
    close $fh;
    my @results;
    foreach( @lines ){
        push( @results, $fusion->query( $_ ) );
    }
    return \@results;
}
